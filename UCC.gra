//-----------------------------
//-----------------------------
Helpers:

hDigit = '0' .. '9';
hLetter = 'a' .. 'z' + 'A'..'Z';
hSimbol = hLetter + '_';
hAll = 1 .. 255;
hBlank = ' ' + '\t' + 13;
hBreakLine = 10;

hAllLBreakLine = hAll - 10;
hAllLSQuote = (hAll - '\'') - '\\';

//----------------------------
//----------------------------
Tokens:

// other
identifier = hSimbol (hSimbol | hDigit)*;
semicolon = ';';
to = '..';
comma = ',';
*blank = hBlank+;
*breakLine = hBreakLine;

// comment
*comment = '//' hAllLBreakLine*;

// modes
helpers = 'Helpers:';
tokens = 'Tokens:';
productions = 'Productions:';
reducer = 'Reducer:';

// operator
attrib = '=';
sum = '+';
sub = '-';
mult = '*';
or = '|';
reduce = '<-';
question = '?';

// scope
o_parentheses = '(';
c_parentheses = ')';
o_brackets = '[';
c_brackets = ']';

// literals
charLiteral = '\'' (hAllLSQuote | '\\' hAll)*  '\'';
decimal = hDigit+;

//----------------------------------
//----------------------------------
Productions:

Total = helpers Helpers? tokens Tokens? productions Productions? reducer Reducers?;

Helpers = Helpers Helper | Helper;
Helper = identifier attrib HelperExpr semicolon;
HelperExpr = HelperFactor0 | HelperExpr HelperOperator HelperFactor0;
HelperFactor0 = HLiteral to HLiteral | o_parentheses HelperExpr c_parentheses | HLiteral | identifier;
HelperOperator = sum | sub;

HLiteral = charLiteral | decimal;

Tokens = Tokens Token | Token;
Token = mult? identifier attrib TokenExpr semicolon;
TokenExpr = TokenFactor0 | TokenExpr OrOperator TokenFactor0;
TokenFactor0 = TokenFactor1 | TokenFactor0 TokenFactor1;
TokenFactor1 = TokenFactor2 | TokenFactor1 ModifierOperator;
TokenFactor2 = o_parentheses TokenExpr c_parentheses | identifier | TLiteral;

TLiteral = charLiteral;

OrOperator = or;
ModifierOperator = sum | mult | question;

Productions = Productions Production | Production;
Production = identifier attrib ProductionExpr semicolon;
ProductionExpr = ProductionFactor0 | ProductionExpr OrOperator ProductionFactor0;
ProductionFactor0 = ProductionFactor1 | ProductionFactor0 ProductionFactor1;
ProductionFactor1 = ProductionFactor2 | ProductionFactor2 QuestionOperator;
ProductionFactor2 = identifier;

QuestionOperator = question;

Reducers = Reducers Reducer | Reducer;
Reducer = identifier reduce ReducerExpr semicolon;
ReducerExpr = ReducerFactor0 | ReducerExpr comma ReducerFactor0;
ReducerFactor0 = identifier;

CommaOperator = comma;

//----------------------------------
//----------------------------------
Reducer:

Expr <- HelperExpr, HelperFactor0, TokenExpr, TokenFactor0, TokenFactor1, TokenFactor2, ProductionExpr, ProductionFactor0, ProductionFactor1, ProductionFactor2, ReducerExpr, ReducerFactor0;
Op <- HelperOperator, OrOperator, ModifierOperator, QuestionOperator, CommaOperator;
Literal <- HLiteral, TLiteral;
